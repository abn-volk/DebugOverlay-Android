repositories {
    jcenter()
    flatDir {
        dirs 'libs'
    }
}

apply plugin: 'com.android.application'
// make sure this line comes *after* you apply the Android plugin
apply plugin: 'com.getkeepsafe.dexcount'

def gitHash() {
    return 'git rev-parse --short HEAD'.execute().text.trim()
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        applicationId "com.ms_square.debugoverlay.sample"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion

        versionCode 1
        versionName "1.0.0"

        buildConfigField "String", "GIT_HASH", "\"${gitHash()}\""

        testInstrumentationRunner "com.ms_square.debugoverlay.DebugOverlayTestRunner"
    }

    signingConfigs {
        release {
            def Properties keyProps = new Properties()
            // double check if keystore.properties exists to avoid exception
            if (file("../keystore.properties").exists()) {
                keyProps.load(new FileInputStream(file('../keystore.properties')))
            }
            storeFile keyProps["storeFile"] != null ? file(keyProps["storeFile"]) : null
            storePassword keyProps["storePassword"]
            keyAlias keyProps["keyAlias"]
            keyPassword keyProps["keyPassword"]
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            // check if keystore.properties exists in the root
            if (file("../keystore.properties").exists()) {
                signingConfig signingConfigs.release
            }
        }
    }

    dexOptions {
        javaMaxHeapSize "2g"
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    debugImplementation project(':debugoverlay')
    releaseImplementation project(':debugoverlay-no-op')
    testImplementation project(':debugoverlay-no-op')

    implementation(project(':debugoverlay-ext-timber')) {
        exclude module: 'debugoverlay'
    }

    implementation(project(':debugoverlay-ext-netstats')) {
        exclude module: 'debugoverlay'
    }

    implementation 'androidx.appcompat:appcompat:1.0.0'
    implementation 'com.google.android.material:material:1.0.0'

    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'

    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.5'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5'
    testImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5'

    // Set this dependency to build and run Espresso tests
    androidTestImplementation('androidx.test.espresso:espresso-core:3.1.0', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    // ScreenShot taker for instrumentation tests
    androidTestImplementation(name:'cloudtestingscreenshotter_lib', ext:'aar')

    testImplementation deps.junit
}
